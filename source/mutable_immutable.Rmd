# Mutable and immutable objects


_Warning:_ This topic can be somewhat mind-boggling! While I strive to write correct definitions and explanations, remember that these are simply my notes on using Julia, and I'm not an expert! So take my words and explanations with a grain of salt, 	and drop me an e-mail or fork these notes on github to fix them if you find errors.
	

Objects in Julia can either be mutable or immutable, you can check whether
an object is immutable with the `isimmutable` function, for example, integers and
floating point numbers are immutable:

```{julia}
isimmutable(1)
```

```{julia}
isimmutable(1.1)
```

while arrays and dictionaries are mutable:

```{julia}
isimmutable([1,2,3])
```

```{julia}
isimmutable(Dict("a" => 1, "b" => 2, "c" => 3))
```

Immutable objects cannot be changed, while mutable objects can. Why is this important? Because they behave very differently. For example, suppose that you bind variable `a` to an immutable object:

```{julia}
a = 1
```

then you bind another variable, `b`, to `a`:

```{julia}
b = a
```

you can check that `a` and `b` are bound to the same object:

```{julia}
println(objectid(a))
println(objectid(b))
objectid(a) == objectid(b)
```

now if you change the value of `a`, you're binding it to a new object, while `b` is still bound to the previous one:

```{julia}
a = 2
println(objectid(a))
println(b)
println(objectid(b))

```

so, `a` and `b` now have different values. So far so good, but what happens with variables bound to mutable objects? Let's try to bind `x` to an array, which is mutable, and then bind `y` to `x`:

```{julia}
x = [1,2,3]
y = x
println(objectid(x))
println(objectid(y))
```

now we change the value of the first element of `x`:

```{julia}
x[1] = 999
println(y)
println(objectid(x))
println(objectid(y))

```

we can see that the first element of `y` has changed as well, and `x` and `y` are still bound to the same object. When we modify the value of the first element of `x` we're not binding `x` to a new object, we're modifying the old one, so `x` and `y` are still bound to the same object, and changing the value of the first element of `x` also changes the value of the first element of `y`. But watch out! If you do:

```{julia}
x = [1,2,3]
y = x
println(objectid(x))
println(objectid(y))
x = [999, 2, 3]
println(y)
println(objectid(x))
println(objectid(y))
```

you're not modifying the mutable object to which `x` was bound, you're binding `x` to a totally new object! So `x` and `y` will be bound to different objects.

!!! tip
    It may help to think of a mutable object as a container of items.
	
When you change an item in the container the binding of a variable to the container doesn't change, but if you change the container... the binding will change. Here's anothe example in which we change the container:

```{julia}
x = [1,2,3]
y = x
x = [1,2,3] #it looks the same but we're changing the container!
objectid(y) == objectid(x) #not the same object
```

despite the fact that they have the same value:
```{julia}
x==y
```

All this may seem abstract and, and may seem to be touching edge cases, but it has important consequences. One of these is that if you pass a mutable object to a function as an argument, and you modify that object inside the function, the object will be modified even though you may not explicitly return it. For example, suppose that we write a function to sum two arrays, and in case the first array is longer than the second one we remove elements from it until their length matches:

```{julia, results="hide"}
function foo(x,y)
    while length(x) > length(y)
        pop!(x)
    end
    z = x+y
    return z
end    
```

note that the function does not explicitly return `x`, but will nontheless modify it. Now let's call the function with the following arguments:

```{julia}
x = [1,1,1,1];
y = [2,2];
z = foo(x, y)
```

we get the value of `z` which we explicitly returned, but if we check what value now `x` has:

```{julia}
x
```

we can see that it has been modified as well. This doesn't happen with immutable objects. Let's check it with another very contrived example:

```{julia, results="hide"}
function foo(x,y)
    while x > y
        x = x-1
    end
    z = x+y
    return z
end    
```

```{julia}
x = 8; y=3;
foo(x, y)
```

the function returns the expected value, and if we check what's the value of `x`:

```{julia}
x
```

we can see that it has not changed. That's because `x` is bound to an immutable object in this case, and the variable `x` inside the function is in a different scope from the variable `x` outside the function.


_Warning:_ The moral of the story above is: Watch out when you pass mutable objects as function arguments!
	
Another important side effect of the difference between mutable and immutable objects involves the local scope introduced by certain code blocks, such as `for` loops. Variables from the global scope are available for reading in the local scope, but not for writing. So for example the `x` in the `for` loop below will be bound to a new object in the local scope of the loop:

```{julia}
x = 0;
for i=1:3
    x = i
    println(x)
end
```

we can check that the `x` in the global scope has not been modified:

```{julia}
x
```

But if `x` is a mutable object, and we modify one of its elements inside the loop:

```{julia}
x = [0,0,0];
for i=1:3
    x[2] = i
    println(x)
end
```

this will be reflected in the global scope:

```{julia}
x
```
